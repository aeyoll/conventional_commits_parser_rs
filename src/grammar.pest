summary_content = { (!NEWLINE ~ ANY)+ }

message = { SOI ~ summary ~ body? ~ footers? ~ EOI }

// <type>[optional scope]: <description>
summary = ${ commit_type ~ scope? ~ breaking_change_mark? ~ type_separator ~ whitespace_terminal ~ summary_content }
commit_type = { ASCII_ALPHA+ }
scope = {  (parent_left ~ scope_content ~ parent_right) }
breaking_change_mark = { "!"? }
scope_content = ${ ( !parent_right ~ !parent_left ~ !NEWLINE ~ ANY)+ }


// [optional body]
body = { (continuation ~ !footer ~ body_content) }
body_content = { (!line_separator ~ ANY)+ }

// [optional footer(s)]
footers = { continuation ~ footer* }
footer = ${ NEWLINE* ~ token ~ token_separator ~ footer_content }
breaking_change_token = { "BREAKING CHANGE" | "BREAKING-CHANGE" }
token = { breaking_change_token | ( !token_separator ~ ( ASCII_ALPHANUMERIC | "-"))* }
token_separator = { ": " | " #" }
footer_content = { (!footer ~ ANY)+ }

type_separator = { ":" }
whitespace_terminal = { " " }
parent_left = _{ "(" }
parent_right = _{ ")" }
WHITESPACE = _{ " " }
blank_line = _{ NEWLINE ~ WHITESPACE*}
line_separator = _{ NEWLINE ~ WHITESPACE* ~ NEWLINE}
continuation = _{ blank_line ~ NEWLINE }